---
eip: 6105
title: Marketplace Extension for EIP-721
description: Adds a basic marketplace functionality to EIP-721.
author: Silvere Heraudeau (@lambdalf-dev), Martin McConnell (@offgridgecko), 5660-eth (@5660-eth), Abu <team10kuni@gmail.com>, Wizard Wang
discussions-to: https://ethereum-magicians.org/t/idea-a-marketplace-extension-to-erc721-standard/11975
status: Draft
type: Standards Track
category: ERC
created: 2022-12-02
requires: 165, 721
---

## Abstract

Add a basic marketplace functionality to [EIP-721](./eip-721.md)
to enable non-fungible token trading without relying on an intermediary trading platform.


## Motivation

Most current NFT trading relies on an NFT trading platform acting as an intermediary, which has the following problems:

1. Security concerns arise from authorization via the `setApprovalForAll` function. The permissions granted to NFT trading platforms expose unnecessary risks. Should a problem occur with the trading platform contract, it would result in significant losses to the industry as a whole. Additionally, if a user has authorized the trading platform to handle their NFTs, it allows a phishing scam to trick the user into signing a message that allows the scammer to place an order at a low price on the NFT trading platform and designate themselves as the recipient. This can be difficult for ordinary users to guard against.
2. High trading costs are a significant issue. On one hand, as the number of trading platforms increases, the liquidity of NFTs becomes dispersed. If a user needs to make a deal quickly, they must authorize and place orders on multiple platforms, which increases the risk exposure and requires additional gas expenditures for each authorization. For example, taking BAYC as an example, with a total supply of 10,000 and over 6,000 current holders, the average number of BAYC held by each holder is less than 2. While `setApprovalForAll` saves on gas expenditure for pending orders on a single platform, authorizing multiple platforms results in an overall increase in gas expenditures for users. On the other hand, trading service fees charged by trading platforms must also be considered as a cost of trading, which are often much higher than the required gas expenditures for authorization.
3. Aggregators provide a solution by aggregating liquidity, but the decision-making process is centralized. Furthermore, as order information on trading platforms is off-chain, the aggregator's efficiency in obtaining data is affected by the frequency of the trading platform's API and, at times, trading platforms may suspend the distribution of APIs and limit their frequency.
4. The project parties' copyright tax income is dependent on centralized decision-making by NFT trading platforms. Some trading platforms disregard the interests of project parties and implement zero copyright tax, which is a violation of their interests.
5. NFT trading platforms are not resistant to censorship. Some platforms have delisted a number of NFTs and the formulation and implementation of delisting rules are centralized and not transparent enough. In the past, some NFT trading platforms have failed and wrongly delisted certain NFTs, leading to market panic.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL
NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY",
and "OPTIONAL" in this document are to be interpreted as described in RFC 2119
and RFC 8174.

Compliant contracts MUST implement the following interface:

```solidity

interface IERC6105 {

  /// @notice Emitted when a token is listed for sale or delisted.
  /// @dev The zero price indicates that the token is not for sale
  ///      The zero expires indicates that the token is not for sale
  /// @param tokenId - the NFT asset being listed
  /// @param from - address of who is selling the token
  /// @param to - address of who this listing is for, 
  ///             can be address zero for a public listing,
  ///             or non zero address for a private listing 
  /// @param price - the price the token is being sold for, regardless of currency
  /// @param expires - UNIX timestamp, the buyer could buy the token before expires
  event UpdateListing(uint256 indexed tokenId, address indexed from, address indexed to, uint256 price ,uint64 expires);

  /// @notice Emitted when a token that was listed for sale is being purchased.
  /// @param tokenId - the NFT asset being purchased
  /// @param from - address of who is selling the token
  /// @param to - address of who is buying the token 
  /// @param price - the price the token is being sold for, regardless of currency
  event Purchased(uint256 indexed tokenId, address indexed from, address indexed to, uint256 price);

  /// @notice Lists token `tokenId` for sale
  /// @dev `price` MUST NOT be set to zero
  /// @param tokenId - the NFT asset being listed
  /// @param price - the price the token is being sold for, regardless of currency
  /// @param expires - UNIX timestamp, the buyer could buy the token before expires
  /// @param to - address of who this listing is for, 
  ///             can be address zero for a public listing,
  ///             or non zero address for a private listing 
  /// Requirements:
  /// - `tokenId` must exist
  /// - Caller must be owner, authorised operators or approved address of the token
  /// - `price` must not be zero
  /// - Must emit a {UpdateListings} event.
  function listItem(uint256 tokenId, uint256 price, uint64 expires, address to) external;
 
  /// @notice Delists token `tokenId` that was listed for sale
  /// @param tokenId - the NFT asset being delisted
  /// Requirements:
  /// - `tokenId` must exist and be listed for sale
  /// - Caller must be owner, authorised operators or approved address of the token
  /// - Must emit a {UpdateListings} event.
  function delistItem(uint256 tokenId) external;
 
  /// @notice Buys a token and transfers it to the caller.
  /// @param tokenId - the NFT asset being purchased
  /// Requirements:
  /// - `tokenId` must exist and be listed for sale
  /// - Caller must be able to pay the listed price for `tokenId`
  /// - Must emit a {Purchased} event.
  function buyItem(uint256 tokenId) external payable;

  /// @notice Returns the listing for `tokenId`
  /// @dev The zero price indicates that the token is not for sale
  ///      The zero expires indicates that the token is not for sale
  ///      The zero address indicates that the token is for a public listing
  /// @return the specified listing (price, expires, intended recipient)
  function getListing(uint256 tokenId) external view returns (uint256, uint64, address);
}
```

The `listItem(uint256 tokenId, uint256 price, uint64 expires, address to)` function MAY be implemented as `public` or `external`.And the `price` in this function MUST NOT be set to zero.

The `delistItem(uint256 tokenId)` function MAY be implemented as `public` or `external`.

The `buyItem(uint256 tokenId)` function MUST be implemented as `payable` and MAY be implemented as `public` or `external`.

The `getListing(uint256 tokenId)` function MAY be implemented as `pure` or `view`.

The `UpdateListing` event MUST be emitted when a token is listed for sale or delisted.

The `Purchased` event MUST be emitted when a token is traded.

The `supportsInterface` method MUST return `true` when called with `0x6de8e04d`.

## Rationale

## Backwards Compatibility

This standard is compatible with [EIP-721](./eip-721.md) and [EIP-2981](./eip-2981.md).

## Reference Implementation

```solidity
 // SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/common/ERC2981.sol";
import "./IERC6105.sol";

contract ERC6105 is ERC721, ERC2981, IERC6105 {

  /// @dev A structure representing a listed token
  ///      The zero price indicates that the token is not for sale
  ///      The zero expires indicates that the token is not for sale
  /// @param price - the price the token is being sold for, regardless of currency
  /// @param expires - UNIX timestamp, the buyer could buy the token before expires
  /// @param to - address of who this listing is for, 
  ///             can be address zero for a public listing,
  ///             or non zero address for a private listing 
  struct Listing {
    uint256 price;
    uint64 expires;
    address to;
  }

  // Mapping from token ID to listing index
  mapping(uint256 => Listing) private _listings;

  constructor(string memory name_, string memory symbol_)
    ERC721(name_, symbol_)
    {
    }

/// @dev Create or update a listing for `tokenId`.
///      Setting `buyer` to the NULL address will create a public listing.
/// @param tokenId    : identifier of the token being listed
/// @param price      : the sale price of the token being listed
/// @param tokenOwner : current owner of the token
/// @param expires - UNIX timestamp, the buyer could buy the token before expires
/// @param buyer      : optional address the token is being sold too
  function _listItem(uint256 tokenId, uint256 price, address tokenOwner, uint64 expires, address buyer) internal {
    _listings[tokenId].price = price;
    _listings[tokenId].expires = expires;
    _listings[tokenId].to = buyer;
    emit UpdateListing(tokenId, tokenOwner, buyer,  price, expires);
  }

/// @dev Removes the listing for `tokenId`.
/// @param tokenId : identifier of the token being listed
  function _removeListing(uint256 tokenId) internal {
    address tokenOwner = ownerOf(tokenId);
    delete _listings[tokenId];
    emit UpdateListing(tokenId, tokenOwner, address(0),  0, 0);
  }

/// @dev Processes an ether of `amount` payment to `recipient`.
/// @param amount    : the amount to send
/// @param recipient : the payment recipient
  function _processEthPayment(uint256 amount, address recipient) internal {
    (bool success,) = payable(recipient).call{value: amount}("");
    require(success, "Ether Transfer Fail");
  }

/// @notice Create or update a listing for `tokenId`.
///         Setting `buyer` to the NULL address will create a public listing.
/// @param tokenId : identifier of the token being listed
/// @param expires : UNIX timestamp, the buyer could buy the token before expires
/// @param price   : the sale price of the token being listed
/// @param buyer   : optional address the token is being sold too
  function listItem (uint256 tokenId, uint256 price,uint64 expires, address buyer) external {
    address tokenOwner = ownerOf(tokenId);
    require(_isApprovedOrOwner(_msgSender(), tokenId),"ERC6105: caller is not owner nor approved");

   _listItem(tokenId, price, tokenOwner,expires, buyer);
  }

  function _isForSale(uint256 tokenId) internal virtual returns(bool){

    if(_listings[tokenId].price > 0 && _listings[tokenId].expires >=  block.timestamp)
    {
        return true;
    }
    else{
        return false;
    }    
  }
  
/// @notice Removes the listing for `tokenId_`.
/// @param tokenId : identifier of the token being listed
  function delistItem(uint256 tokenId) external {
    require(_isApprovedOrOwner(_msgSender(), tokenId),"ERC6105: caller is not owner nor approved");
    require(_isForSale(tokenId), "ERC6105: invalid listing" );

    _removeListing(tokenId);
  }

/// @notice Purchases the listed token `tokenId_`.
/// @param tokenId : identifier of the token being purchased
  function buyItem(uint256 tokenId) external payable {
    address tokenOwner = ownerOf(tokenId);
    address buyer = msg.sender;
    uint256 value = msg.value;
    uint256 price = _listings[tokenId].price;
    require(_isForSale(tokenId), "ERC6105: invalid listing");
    require(
      buyer == _listings[tokenId].to ||
      _listings[tokenId].to == address(0),
      "ERC6105: invalid sale address"
    );
    require(value == price, "ERC6105: incorrect price");

    _transfer(tokenOwner, buyer, tokenId);
    emit Purchased(tokenId, tokenOwner, buyer,  price);

    /// @dev Handle royalties
    (address royaltyRecipient, uint256 royalties) = royaltyInfo(tokenId, msg.value);
    _processEthPayment(royalties, royaltyRecipient);

    uint256 payment = msg.value - royalties;
    _processEthPayment(payment, tokenOwner);
  }

  /// @notice Returns the listing for `tokenId_`
  /// @dev The zero price indicates that the token is not for sale
  ///      The zero expires indicates that the token is not for sale
  ///      The zero address indicates that the token is for a public listing
  /// @return the specified listing (price, expires, intended recipient)
  function getListing(uint256 tokenId) external view returns (uint256, uint64, address) {
    uint256 price = _listings[tokenId].price;
    uint64 expires = _listings[tokenId].expires;
    address to = _listings[tokenId].to;
    return (price, expires, to);
  }

   /// @dev See {IERC165-supportsInterface}.
  function supportsInterface(bytes4 interfaceId) public view virtual override (ERC721, ERC2981) returns (bool) {
     return interfaceId == type(IERC6105).interfaceId || super.supportsInterface(interfaceId);
  }

  function _beforeTokenTransfer(address from,address to,uint256 tokenId,uint256 batchSize) internal virtual override{
      super._beforeTokenTransfer(from, to, tokenId, batchSize);
      if(_isForSale(tokenId)){
          delete  _listings[tokenId];
          emit UpdateListing(tokenId, to, address(0),  0, 0);
      }
  }
}
        
```

## Security Considerations

Needs discussion.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
