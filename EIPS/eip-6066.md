# EIP-6066: Standard Signature Validation Method for NFTs

Standard way to verify a signature when the signing entity is an NFT.

## Abstract

While **E**xternally **O**wned **A**ccounts can validate signed messages with `ecrecover()` and smart contracts can validate signatures using specifications outlined in EIP-1271, currently there is no standard method to create or validate signatures made by NFTs. We propose a standard way for anyone to validate whether a signature made by an NFT is valid. This is possible via a modified signature validation function originally found in EIP-1271: `isValidSignature(tokenId, hash)`.

## Motivation

With billions of ETH in trading volume, the **N**on-**F**ungible **T**oken standard has exploded into tremendous popularity in recent years. Despite the far-reaching implications of having unique tokenized items on-chain, NFTs have mainly been used to represent artwork in the form of avatars or profile pictures. While this is certainly not a trivial use case for the EIP-721 & EIP-1155 token standards, we reckon more can be done to aid the community in discovering alternative uses for NFTs, one of which is using an NFT to represent a certain office in an organization. In this case, tying signatures to transferrable NFTs instead of EOAs or smart contracts becomes crucial.

Suppose there exists a DAO that utilizes NFTs as badges that represent certain administrative offices (i.e., CEO, COO, CFO, etc.) with a quarterly democratic election that potentially replaces those who currently occupy said offices. If the sitting COO has previously signed agreements or authorized certain actions, their past signatures would stay with the EOA who used to be the COO instead of the COO's office itself once they are replaced with another EOA as the new COO-elect. Although a multisig wallet for the entire DAO is one way to mitigate this problem, often it is helpful to generate signatures on a more intricate level so detailed separation of responsibilities are established and maintained. It is also feasible to appoint a smart contract instead of an EOA as the COO, but the complexities this solution brings are unnecessary. If a DAO uses ENS to establish their organizational hierarchy, this proposal would allow wrapped ENS subdomains (which are NFTs) to generate signatures.

We have purposefully decided to not include a signature generation standard in this proposal as it would restrict flexibility of such mechanism, just as EIP-1271 does not enforce a signing standard for smart contracts.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119.

```
pragma solidity ^0.8.0;

interface IEIP6066 {
    /**
     * @dev Should return whether the signature provided is valid for the provided tokenId and hash
     * @param tokenId   Token ID of the signing NFT
     * @param hash      Hash of the data to be signed
     *
     * MUST return the bytes4 magic value 0x1e6395e6 when function passes.
     * MUST NOT modify state (using STATICCALL for solc < 0.5, view modifier for solc > 0.5)
     * MUST allow external calls
     *
     */
    function isValidSignature(
        uint256 tokenId,
        bytes32 hash
    ) external view returns (bytes4 magicValue);
}
```

`isValidSignature` can call arbitrary methods to validate a given signature.

This function should be implemented by EIP-721 or EIP-1155 compliant contracts that desire to enable its owners to sign messages using their NFTs. Applications wanting to support contract signatures should call this method if the signer is an NFT.

## Backwards Compatibility

This EIP is incompatible with previous work on signature validation as it does not validate any cryptographically generated signatures. Instead, signature is merely a boolean flag indicating consent. This is consistent with Gnosis Safe's contract signature implementation.

## Reference Implementation

Example implementation of an EIP-721 compliant contract that conforms to EIP-6066 with a custom signing function:

```
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./interfaces/draft-IEIP6066.sol";

contract EIP6066Reference is ERC721, IEIP6066 {
    bytes4 public constant MAGICVALUE = 0x1e6395e6;
    bytes4 public constant BADVALUE = 0xffffffff;

    mapping(uint256 => mapping(bytes32 => bool)) internal _signatures;

    error ENotTokenOwner();

    /**
     * @dev Checks if the sender owns NFT with ID tokenId
     * @param tokenId   Token ID of the signing NFT
     */
    modifier onlyTokenOwner(uint256 tokenId) {
        if (ownerOf(tokenId) != _msgSender()) revert ENotTokenOwner();
        _;
    }

    constructor(string memory name_, string memory symbol_)
        ERC721(name_, symbol_)
    {}

    /**
     * @dev Should sign the provided hash with NFT of tokenId given sender owns said NFT
     * @param tokenId   Token ID of the signing NFT
     * @param hash      Hash of the data to be signed
     */
    function sign(uint256 tokenId, bytes32 hash)
        external
        onlyTokenOwner(tokenId)
    {
        _signatures[tokenId][hash] = true;
    }

    /**
     * @dev Should return whether the signature provided is valid for the provided tokenId and hash
     */
    function isValidSignature(uint256 tokenId, bytes32 hash)
        external
        view
        override
        returns (bytes4 magicValue)
    {
        return _signatures[tokenId][hash] ? MAGICVALUE : BADVALUE;
    }
}
```

## Notes

The revokable nature of contract-based signatures carries over to this EIP standard. Developers and users alike should take this into consideration.

## References

- EIP-1271: Standard Signature Validation Method for Contracts: https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1271.md
- SignMessageLib from Gnosis Safe: https://github.com/safe-global/safe-contracts/blob/main/contracts/libraries/SignMessageLib.sol
