---
eip: 3712
title: Multiple Fungible-Tokens
description: Supports the creation of multiple homogeneous tokens in the same contract, supports batch transfers, and global authorization.
author: Zhao Li (@1999321), Derek Zhou (@zhous), Yuefei Tan (@whtyfhas)
discussions-to: https://ethereum-magicians.org/t/eip-3712-standard-for-multiple-types-of-fungible-tokens/6912
status: Draft
type: Standards Track
category: Interface
created: 2021-08-01
---

## Abstract

This standard covers some situations that the [ERC-20](./eip-20.md) standard and [ERC-1155](./eip-1155.md) standard can hardly handle. It allows multiple types of fungible tokens to be sent to multiple receiving addresses via our proposed interface `transferBatchMul` and allows multiple types of fungible tokens to be sent from multiple sending addresses to multiple receiving addresses via our proposed interface `transferFromBatchMul`. This standard greatly reduces the gas consumption in transactions of multiple types of fungible tokens. In addition the standard allows both setting of an approval for a single token via an interface `approve(uint256 id,address spender, uint256 amount)` and setting of approvals uniformly for all types of  tokens via an interface `approve(address spender,bool _status)`.

## Motivation

The [ERC-20](./eip-20.md) token standard defines a single fungible tokenâ€™s attributes and interfaces. When interacting with multiple types of fungible tokens, multiple token contracts each of which defines a single token need to be developed and deployed. The [ERC-1155](./eip-1155.md) token standard combines both fungible tokens and non-fungible tokens but lacks both an interface to set an approval for a spender for an id specified type of fungible token and an interface to set an approval for multiple spenders for an id specified type of fungible token. In addition both the [ERC-20](./eip-20.md) and the [ERC-1155](./eip-1155.md) token standards lack interfaces to transact from multiple sending addresses to multiple receiving addresses. This proposed standard covers these use cases and allows multiple types of fungible tokens to be transacted from multiple sending addresses to multiple receiving addresses.   

## Specification



```solidity
// CC0 1.0 Universal (CC0 1.0) Public Domain Dedication
pragma solidity ^0.8.0;

/**
@param name eip3712 token name
@param symbol eip3712 token symbol
@param totalSupply eip3712 total supply
@param decimals eip3712 decimals 
*/
struct EIP3712Info{
    string name;
    string symbol;
    uint256 totalSupply;
    uint8 decimals;
}

/**
 * @title IEIP3712
 * @dev Interface for the EIP3712 token standard.
 */
interface IEIP3712 {

    /**
    *@notice Retrieves the EIP3712Info structure for a given EIP3712 token ID.
    *@param eip3712_id The ID of the EIP3712 token.
    *@return The EIP3712Info structure.
    */
    function eip3712Infos(uint eip3712_id) external view returns(EIP3712Info memory);

    /**
    *@notice Retrieves the balance of a specific EIP3712 token for a given account.
    *@param eip3712_id The ID of the EIP3712 token.
    *@param account The address of the account.
    *@return The balance of the EIP3712 token for the account.
    */
    function balanceOf(uint eip3712_id, address account) external view returns(uint);

    /**
    *@notice Transfers a specific amount of an EIP3712 token to a recipient.
    *@param eip3712_id The ID of the EIP3712 token.
    *@param recipient The address of the recipient.
    *@param amount The amount to transfer.
    *@return True if the transfer is successful, false otherwise.
    */
    function transfer(uint eip3712_id,address recipient, uint amount) external returns(bool);
    
    /**
    *@notice Transfers multiple EIP3712 tokens to multiple recipients.
    *@param eip3712_ids The IDs of the EIP3712 tokens.
    *@param recipients The addresses of the recipients.
    *@param amounts The amounts to transfer.
    *@return True if the transfer is successful, false otherwise.
    */
    function transferBatch(uint[] memory eip3712_ids,address[]memory recipients,uint[] memory amounts) external returns(bool);

    /**
    *@notice Retrieves the allowance for a specific spender on a specific EIP3712 token for a given owner.
    *@param eip3712_id The ID of the EIP3712 token.
    *@param owner The address of the owner.
    *@param spender The address of the spender.
    *@return The allowance for the spender on the EIP3712 token for the owner.
    */
    function allowances(uint eip3712_id,address owner, address spender) external view returns(uint);

    /**
    *@notice Approves a specific spender to spend a specific amount of an EIP3712 token on behalf of the owner.
    *@param eip3712_id The ID of the EIP3712 token.
    *@param spender The address of the spender.
    *@param amount The amount to approve.
    *@return True if the approval is successful, false otherwise.
    */
    function approve(uint eip3712_id,address spender, uint amount) external returns (bool);
    
    /**
    *@notice Approves or revokes the global approval for a specific spender.
    *@param spender The address of the spender.
    *@param status The approval status (true for approval, false for revocation).
    *@return True if the approval status is successfully updated, false otherwise.
    */
    function approveGlobal(address spender,bool status) external returns(bool);
    
    /**
    *@notice Transfers multiple EIP3712 tokens from multiple senders to multiple recipients.
    *@param eip3712_ids The IDs of the EIP3712 tokens.
    *@param senders The addresses of the senders.
    *@param recipients The addresses of the recipients.
    *@param amounts The amounts to transfer.
    *@return True if the transfer is successful, false otherwise.
    */
    function transferFromBatch(uint[] memory eip3712_ids,address[]memory senders,address[]memory recipients,uint[] memory amounts) external returns(bool);

    /**
    *@notice Transfers a specific amount of an EIP3712 token from the sender to a recipient.
    *@param eip3712_id The ID of the EIP3712 token.
    *@param sender The address of the sender.
    *@param recipient The address of the recipient.
    *@param amount The amount to transfer.
    *@return True if the transfer is successful, false otherwise.
    */
    function transferFrom(uint eip3712_id,address sender, address recipient, uint amount) external returns(bool);

    /**
    *@notice Emitted when a transfer of an EIP3712 token occurs.
    *@param eip3712_id The ID of the EIP3712 token.
    *@param from The address from which the transfer is made.
    *@param to The address to which the transfer is made.
    *@param value The amount transferred.
    */
    event Transfer(uint eip3712_id,address indexed from, address indexed to, uint value);

    /**
    *@notice Emitted when an approval of an EIP3712 token occurs.
    *@param eip3712_id The ID of the EIP3712 token.
    *@param owner The address that approves the spender.
    *@param spender The address that is approved.
    *@param value The amount approved.
    */
    event Approval(uint eip3712_id,address indexed owner, address indexed spender, uint value);
    
    /**
    *@notice Emitted when a batch transfer of EIP3712 tokens occurs.
    *@param eip3712_ids The IDs of the EIP3712 tokens.
    *@param from The addresses from which the transfers are made.
    *@param to The addresses to which the transfers are made.
    *@param value The amounts transferred.
    */
    event TransferBatch(uint[] eip3712_ids,address[] from,address[] to,uint[] value);
    
    /**
    *@notice Emitted when a batch approval of EIP3712 tokens occurs.
    *@param eip3712_ids The IDs of the EIP3712 tokens.
    *@param from The addresses from which the approvals are made.
    *@param to The addresses to which the approvals are made.
    *@param value The amounts approved.
    */
    event ApproveBatch(uint[] eip3712_ids,address[] from,address[] to,uint[] value);

    /**
    *@notice Emitted when a new EIP3712 token is created.
    *@param eip3712_id The ID of the new EIP3712 token.
    */
    event CreateEIP3712(uint eip3712_id);
}
```



Each of the interfaces `totalSupply`,`balanceOf`,`transfer`,`allowance`,`transferFrom`,`approve(uint256 id,address spender, uint256 amount)`  introduces a new parameter id  in the [ERC-20](./eip-20.md)'s corresponding interface's parameter list to specify a fungible token's type. 


`approve(address spender,bool status)` is similar to [ERC-1155](./eip-1155.md)'s interface. It allows an approval to be set uniformly for all types of tokens.

`transferBatch` and `transferFromBatch` remove the parameter bytes memory data that the [ERC-1155](./eip-1155.md) standard uses and implement batch transactions.

`transferBatchMul` implements transactions of multiple types of fungible tokens to multiple receiving addresses.

`transferFromBatchMul` implements transactions of multiple types of fungible tokens from multiple sending addresses to multiple receiving addresses.


## Rationale

The [ERC-20](./eip-20.md) token standard defines a single fungible token. The [ERC-1155](./eip-1155.md) token standard defines multiple types of tokens and specifies each token type by using a parameter `id`. By  using this parameter `id`, the [ERC-1155](./eip-1155.md) token standard greatly reduces the gas consumption in transactions of multiple types of tokens, compared with the [ERC-20](./eip-20.md) token standard. Therefore our proposed standard uses a parameter `id` as well to specify a token type in order to reduce the gas consumption for  transactions of multiple types of tokens in one call operation via `transferBatch` or `transferFromBatch`. In our proposed st andard, each type of fungible token is specified by an id, therefore we add an interface `approve(uint256 id,address spender, uint256 amount)` for applications to set an approval for the id specified type of fungible token. In order to set approvals for multiple types of fungible tokens in minimum operations we introduce a function `approve(address spender,bool _status)`  to set approvals uniformly for all types of fungible tokens in one call operation. To handle a transaction of multiple types of fungible tokens from one sending address to multiple receiving addresses we introduce an interface `transferBatchMul` . To handle a transaction of multiple types of fungible tokens from multiple sending addresses to multiple receiving addresses we introduce an interface `transferFromBatchMul` .     

## Backwards Compatibility

Our proposed standard strictly adheres to the EIP rules and specifications, therefore our standard is backwork compatible with existing EIPs. Part of the transfer function is compatible with [ERC-1155](./eip-1155.md) but not [ERC-20](./eip-20.md).

## Reference Implementation

Example implementation of a signing contract:



```solidity
// CC0 1.0 Universal (CC0 1.0) Public Domain Dedication
pragma solidity ^0.8.0;
import "../interfaces/IEIP3712.sol";


/**
@dev EIP3712 Basic implementation version
 */
contract EIP3712 is IEIP3712{

    mapping(uint => EIP3712Info) private _eip3712Infos;
    mapping(uint => mapping(address => uint)) private balances;
    mapping(uint => mapping(address => mapping(address => uint))) public override allowances;

    mapping(address => mapping(address => bool)) public allowanceGlobal;

    /**
    @dev whether the eip3712 id is available 
    */
    modifier isInUse(uint eip3712_id) {
        require(_eip3712Infos[eip3712_id].totalSupply > 0,"DAism.EIP3712: id has not issued");
        _;
    }

    /**
    @dev whether a batch of eip3712 id are available
     */
    modifier isInUseBatch(uint[] memory eip3712_ids) {
        for(uint i = 0;i < eip3712_ids.length;i++)
            require(_eip3712Infos[eip3712_ids[i]].totalSupply > 0,"DAism.EIP3712: id has not issued.(batch)");
        _;
    }

    /**
    @dev Authorization address uses token
    @param eip3712_id eip3712 id
    @param spender authorized address
    @param amount Authorized quantity
    */
    function approve(uint eip3712_id,address spender, uint amount) external isInUse(eip3712_id) override returns (bool){
        _approve(eip3712_id,msg.sender,spender,amount);
        return true;
    }
    
    /**
    @dev Authorization address uses all eip3712 token
    @param spender authorized address
    @param permissions if true,authorized address can use all eip3712 token.
    */
    function approveGlobal(address spender,bool permissions) external  override returns(bool) {
        allowanceGlobal[msg.sender][spender] = permissions;
        return true;
    }

    /**
    @param account Asked address
     */
    function balanceOf(uint eip3712_id, address account) external view isInUse(eip3712_id) override returns(uint) {
        return balances[eip3712_id][account];
    }

    /**
    @dev transfer eip3712 token to recipient address
    @param amount transfer amount
     */
    function transfer(uint eip3712_id,address recipient, uint amount) external isInUse(eip3712_id) override returns(bool) {
        _transfer(eip3712_id,msg.sender,recipient,amount);
        return true;
    }
    
    /**
    @dev owner transfer a batch of eip3712 tokens to recipients
    @param eip3712_ids a batch of eip3712_id
    @param recipients a batch of recipients
    @param amounts a batch of transfer amounts
    @notice The parameter length is the same, one-to-one correspondence
     */
    function transferBatch(uint[] memory eip3712_ids,address[]memory recipients,uint[] memory amounts) external override isInUseBatch(eip3712_ids) returns(bool) {
        require(eip3712_ids.length == recipients.length && eip3712_ids.length == amounts.length,"DAism.EIP3712.transferBatch: lengths of transfer's params are not equal.");
        address[] memory senders = new address[](eip3712_ids.length);
        address _msg = msg.sender;
        for(uint i = 0;i < eip3712_ids.length; i++){
            senders[i] = _msg;
        }
        _transferBatch(eip3712_ids, senders, recipients, amounts);
        return true;
    }

    /**
    @dev spender transfer sender's eip3712 token to recipient address
     */
    function  transferFrom(uint eip3712_id,address sender, address recipient, uint amount) external override isInUse(eip3712_id) returns(bool) {
        if(allowanceGlobal[sender][msg.sender]){
            _transfer(eip3712_id,sender,recipient,amount);
        }
        else{
            uint allowance = allowances[eip3712_id][sender][msg.sender];
            require(allowance >= amount,"DAism.EIP3712.transferFrom: allowances not enough");
            _transfer(eip3712_id,sender,recipient,amount);
            _approve(eip3712_id,sender,msg.sender,allowance - amount);
        }
        return true;
    }
    
    /**
    @dev spender transfer a batch of sender's eip3712 tokens to recipients
    @param eip3712_ids a batch of eip3712_id
    @param senders a batch of senders
    @param recipients a batch of recipients
    @param amounts a batch of transfer amounts
    @notice The parameter length is the same, one-to-one correspondence
     */
    function transferFromBatch(uint[] memory eip3712_ids,address[] memory senders,address[] memory recipients,uint[] memory amounts)external override isInUseBatch(eip3712_ids) returns(bool){
        require(eip3712_ids.length == amounts.length && senders.length == recipients.length && senders.length == amounts.length,"DAism.EIP3712._transferBatchMul: length not equal");
        uint[] memory allowance_after = new uint[](eip3712_ids.length);
        address[] memory spenders = new address[](eip3712_ids.length);
        address _msg = msg.sender;

        for(uint i = 0;i < eip3712_ids.length; i++){
            spenders[i] = _msg;
            if(!allowanceGlobal[senders[i]][_msg]){
                uint allowance_i = allowances[eip3712_ids[i]][senders[i]][_msg];
                require(allowance_i >= amounts[i],"DAism.EIP3712._transferBatchMul: insufficient allowance");
                allowances[eip3712_ids[i]][senders[i]][_msg] = allowance_i - amounts[i];
                allowance_after[i] = allowance_i - amounts[i];
            }
                
        }
        _transferBatch(eip3712_ids, senders, recipients, amounts);

        emit ApproveBatch(eip3712_ids, senders, spenders, allowance_after);

        return true;
    }

    /**
    requirements:
    '-' sender cannot be zero address.
    '-' recipient cannot be zero address
    @notice if {sender} is equal to {recipient} ,just return.
     */
    function _transfer(uint eip3712_id,address sender, address recipient, uint amount) internal virtual {
        require(sender != address(0), "DAism.EIP3712._transfer: transfer from the zero address");
        require(recipient != address(0), "DAism.EIP3712._transfer: transfer to the zero address");
        
        if(sender == recipient)
            return;
        uint balance = balances[eip3712_id][sender];
        require(balance >= amount,"DAism.EIP3712._transfer: Insufficient balance");
        balances[eip3712_id][sender] = balance - amount;
        balances[eip3712_id][recipient] = balances[eip3712_id][recipient] + amount;
        emit Transfer(eip3712_id, sender, recipient, amount);
    }
    
    /**
    requirements:
    '-' {sender[i]} cannout be zero address
    '-' {recipient[i]} cannot be zero address
    @notice  if {senders[i]} is equal to {recipient[i]}, just countinue.
    */
    function _transferBatch(uint[] memory eip3712_ids,address[] memory senders,address[] memory recipients,uint[] memory amounts) internal virtual{
        
        for(uint i = 0;i < eip3712_ids.length;i++){
            require(senders[i] != address(0),"DAism.EIP3712._transferBatch:sender is a zero address");
            require(recipients[i] != address(0),"DAism.EIP3712._transferBatch:recipient is a zero address");
            if(amounts[i] == 0 || senders[i] == recipients[i])
                continue;
            uint balance_i = balances[eip3712_ids[i]][senders[i]];
            require(balance_i >= amounts[i],"DAism.EIP3712._transferBatch: Insufficient balance");
            balances[eip3712_ids[i]][senders[i]] = balance_i - amounts[i];
            balances[eip3712_ids[i]][recipients[i]] = balances[eip3712_ids[i]][recipients[i]] + amounts[i];
        }
        emit TransferBatch(eip3712_ids,senders,recipients,amounts);
    }

    /**
    requirements:
    '-' {owner} cannot be zero address
    '-' {spender} cannot be zero address
     */
    function _approve(uint eip3712_id,address owner, address spender, uint amount) internal virtual {
        require(owner != address(0),"DAism.EIP3712._approve:owner is a zero address");
        require(spender != address(0),"DAism.EIP3712._approve:spender is a zero address");

        allowances[eip3712_id][owner][spender] = amount;
        emit Approval(eip3712_id,owner, spender, amount);
    }

    function _mint(uint eip3712_id, address account, uint amount) internal {
        require(account != address(0), "DAism.ERC3712._mint: mint to the zero address");

        _eip3712Infos[eip3712_id].totalSupply += amount;
        unchecked {
            balances[eip3712_id][account] += amount;
        }
        emit Transfer(eip3712_id, address(0), account, amount);
    }

    /**
     * @dev Internal function to create an EIP3712 token.
     * @param eip3712_info The EIP3712Info struct containing the token information.
     * @param _eip3712_id The ID of the EIP3712 token.
     * @return A boolean indicating whether the EIP3712 token creation was successful.
     */
    function _createEip3712(EIP3712Info memory eip3712_info, uint _eip3712_id) internal returns(bool) {
        require(bytes(_eip3712Infos[_eip3712_id].name).length == 0, "EIP3712: id already in use");
        require(bytes(_eip3712Infos[_eip3712_id].symbol).length == 0, "EIP3712: id already in use");
        require(_eip3712Infos[_eip3712_id].totalSupply == 0, "EIP3712: id already in use");
        _eip3712Infos[_eip3712_id] = eip3712_info;
        emit CreateEIP3712(_eip3712_id);
        return true;
    }

    function eip3712Infos(uint eip3712_id) external override view returns (EIP3712Info memory) {
        return _eip3712Infos[eip3712_id];
    }
}
```



## Security Considerations

It has a global authorization function. Compared with ERC20, extra attention needs to be paid to the use of this function. If it is authorized for use by inappropriate addresses, it will cause serious losses.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).

