---
eip: 3561
title: Trust Minimized Upgradeability Proxy
description: proxy with a delay before specified upgrade goes live
author: Sam Porter (@SamPorter1984)
discussions-to: https://ethereum-magicians.org/t/trust-minimized-proxy/5742
status: Stagnant
type: Standards Track
category: ERC
created: 2021-05-09
---

## Simple Summary
Additional storage slots for upgradeability proxy which are assumed to decrease trust in interaction with upgradeable smart contracts.

## Abstract
Removing trust from upgradeability proxy is required for anonymous developers. To achieve that, disallowing instant, potentially malicious upgrades is required. This EIP introduces a time lock, a period of one month, before defined by proxy admin implementation becomes active implementation.

## Motivation
It's usually not possible for anonymous developers who use upgradeability proxies to gain community trust.

Fairer, better future for humanity absolutely requires some developers to stay anonymous while still attract vital attention to solutions they propose and at the same time leverage the benefits of possible upgradeability.

## Specification

The specification is an addition to the standard [EIP-1967](./eip-1967.md) transparent proxy design.
The specification focuses on the slots it adds. All admin interactions with trust minimized proxy must emit an event to make admin actions trackable, and all admin actions must be guarded with `onlyAdmin()` modifier.

### Next Logic Contract Address
Storage slot `0xe8c186b11a4be12af079b0a5c235146db6f3615b2a8b1b47f9bfe3a956337ef9` (obtained as `bytes32(uint256(keccak256('eip3561.proxy.next_implementation')) - 1)`).
Logic address must be first defined as next logic, before it can function as actual logic implementation stored in EIP-1967 `IMPLEMENTATION_SLOT`.
Admin interactions with next logic contract address correspond with these methods and events:
```solidity
// sets next logic contract address and initializes it. Emits NextLogicDefined
// 0x as calldata is an equivalent of proposeTo()
function proposeToAndCall(address implementation, bytes calldata data) external onlyAdmin;
// sets the address stored as next implementation as current IMPLEMENTATION_SLOT
// as soon UPGRADE_BLOCK_SLOT allows
function upgrade() external onlyAdmin;
// cancelling is possible for as long as upgrade() for given next logic was not called
// emits NextLogicCanceled
function cancelUpgrade() external onlyAdmin;

event NextLogicDefined(address indexed nextLogic, uint earliestArrivalBlock); // important to have
event NextLogicCanceled(address indexed oldLogic);
```

### Upgrade Block
Storage slot `0xd366e20ef9f21888e3d225d6a18f0bceb0ce0008a1e881be9a0467a0293afc96` (obtained as `bytes32(uint256(keccak256('eip3561.proxy.upgrade_block')) - 1)`).
On/after this block next logic contract address can be set to EIP-1967 `IMPLEMENTATION_SLOT` or, in other words, start to function as current logic. Updated automatically and is shown as `earliestArrivalBlock` in the event `NextLogicDefined`.

### Propose Block
Storage slot `0x1e166c9744902ecbb9f589bbc9e7da5f078e553ad162c2ee62c71827916db75f` (obtained as `bytes32(uint256(keccak256('eip3561.proxy.propose_block')) - 1)`).
Defines after/on which block *proposing* next logic is possible. Required for convenience, for example can be manually set to a year from given time, so that the users can have peace of mind during the year. Can be set to maximum number to completely seal the code, must not overflow.
Admin interactions with this slot correspond with this method and event:
```solidity
function prolongLock(uint n) external onlyAdmin;
event UpgradesRestrictedUntil(uint block);
```

### Deadline Block
Storage slot `0x560a0645803da084600e483b1076fd54059fdee9045033b88b51d342faf2b2af` (obtained as `bytes32(uint256(keccak256('eip3561.proxy.deadline')) - 1)`).
Defines after/on which block it becomes impossible to upgrade the contract, can be set in constructor if used or can be set many times as long as next value is lower than previous. Required in case of probability that the project development can be compromised, unlike `PROPOSE_BLOCK_SLOT`, deadline is less likely to require any urgent transaction like `PROPOSE_BLOCK_SLOT`.
Admin interactions with this slot should correspond with this method and event:
```solidity
function setDeadline(uint block) external onlyAdmin;
event DeadlineSet(uint block);
```

### Trust_minimized Boolean
Storage slot `0x17dc395ab915c140774b2c05da17fd5b71a2d193e53bc9732d6b62d419a46635` (obtained as `bytes32(uint256(keccak256('eip3561.proxy.trust_minimized')) - 1)`).
False by default and can only ever be set true. While it is false, then the proxy operates exactly as standard EIP-1967 transparent proxy, which allows to correct potential mistakes of first deployment. After set to true, all above specification becomes active.
Admin interactions with this slot should correspond with this method and event:
```solidity
function removeTrust() external onlyAdmin;
event TrustRemoved();
```

## Rationale
An argument "just don't make such contracts upgadeable at all" fails when it comes to complex systems which do or do not heavily reliant on human factor which might manifest itself in unprecedented ways. It might be impossible to model some systems right on first try. Using decentralized governance for upgrade management coupled with EIP-1967 proxy could also become a serious bottleneck for certain protocols before they mature and data is at hand.

A proxy without a time delay before an actual upgrade is obviously abusable. A time delay is probably unavoidable, even if it means that inexperienced developers might not have confidence using it. Albeit this is a downside of this EIP, it's a critically important option to have in smart contract development today.

Propose block adds to convenience if used, so should be kept. An ability to cancel next logic can also be important for the same reason. Deadline block, while can be omitted, can be important for some projects, it would be reasonable to rather keep it too.

## Implementation
```solidity
pragma solidity >=0.7.6;

// EIP-3561 trust minimized proxy implementation https://github.com/ethereum/EIPs/blob/master/EIPS/eip-3561.md

contract TrustMinimizedProxy{
	event Upgraded(address indexed toLogic);
	event AdminChanged(address indexed previousAdmin, address indexed newAdmin);
	event NextLogicDefined(address indexed nextLogic, uint earliestArrivalBlock);
	event ProposingUpgradesRestrictedUntil(uint block, uint nextProposedLogicEarliestArrival);
	event NextLogicCanceled();
	event ZeroTrustPeriodSet(uint blocks);

	bytes32 internal constant ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;
	bytes32 internal constant LOGIC_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;
	bytes32 internal constant NEXT_LOGIC_SLOT = 0x7ab08158deb731c636f84267cdfbd029f6cbf6e8115b44b48b59ae05168d3041;
	bytes32 internal constant NEXT_LOGIC_BLOCK_SLOT = 0x3103c716dde69c1e14393421110d807f5f0067977b2a97cb3ee2d64eacdf93f0;
	bytes32 internal constant PROPOSE_BLOCK_SLOT = 0xbb66d5e599d5e06d0693dafe99957432cd4cb88b1185386f104129a52ce3fb38;
	bytes32 internal constant ZERO_TRUST_PERIOD_SLOT = 0xdc39f6d0a900b897e497b1b6fdc9296b9cb6b290ddae5c838df9defd9bb70abe;

	constructor() payable {
		require(ADMIN_SLOT == bytes32(uint256(keccak256('eip1967.proxy.admin')) - 1) && LOGIC_SLOT==bytes32(uint256(keccak256('eip1967.proxy.implementation')) - 1)
		&& NEXT_LOGIC_SLOT == bytes32(uint256(keccak256('eip3561.proxy.nextLogic')) - 1) && NEXT_LOGIC_BLOCK_SLOT == bytes32(uint256(keccak256('eip3561.proxy.nextLogicBlock')) - 1)
		&& PROPOSE_BLOCK_SLOT == bytes32(uint256(keccak256('eip3561.proxy.proposeBlock')) - 1) && ZERO_TRUST_PERIOD_SLOT == bytes32(uint256(keccak256('eip3561.proxy.trustMinimized')) - 1));
		_setAdmin(msg.sender);
	}

	modifier ifAdmin() {
		if (msg.sender == _admin()) {
			_;
		} else {
			_fallback();
		}
	}

	function _logic() internal view returns (address logic) {
		assembly { logic := sload(LOGIC_SLOT) }
	}

	function _nextLogic() internal view returns (address nextLogic) {
		assembly { nextLogic := sload(NEXT_LOGIC_SLOT) }
	}

	function _proposeBlock() internal view returns (uint bl) {
		assembly { bl := sload(PROPOSE_BLOCK_SLOT) }
	}

	function _nextLogicBlock() internal view returns (uint bl) {
		assembly { bl := sload(NEXT_LOGIC_BLOCK_SLOT) }
	}

	function _zeroTrustPeriod() internal view returns (uint tm) {
		assembly { tm := sload(ZERO_TRUST_PERIOD_SLOT) }
	}

	function _admin() internal view returns (address adm) {
		assembly { adm := sload(ADMIN_SLOT) }
	}

	function _setAdmin(address newAdm) internal {
		assembly { sstore(ADMIN_SLOT, newAdm) }
	}

	function changeAdmin(address newAdm) external ifAdmin {
		emit AdminChanged(_admin(), newAdm);
		_setAdmin(newAdm);
	}

	function upgrade(bytes calldata data) external ifAdmin {
		require(block.number>=_nextLogicBlock(),"too soon");
		address logic;
		assembly {
			logic := sload(NEXT_LOGIC_SLOT) 
			sstore(LOGIC_SLOT,logic)
		}
		(bool success,) = logic.delegatecall(data);
		require(success,"failed to call");
		emit Upgraded(logic);
	}

	fallback () external payable {
		_fallback();
	}

	receive () external payable {
		_fallback();
	}

	function _fallback() internal {
		require(msg.sender != _admin());
		_delegate(_logic());
	}

	function cancelUpgrade() external ifAdmin {
		address logic;
		assembly {
			logic := sload(LOGIC_SLOT)
			sstore(NEXT_LOGIC_SLOT, logic)
		}
		emit NextLogicCanceled();
	}

	function prolongLock(uint b) external ifAdmin {
		require(b > _proposeBlock(),"get maxxed");
		assembly {sstore(PROPOSE_BLOCK_SLOT,b)}
		emit ProposingUpgradesRestrictedUntil(b,b+_zeroTrustPeriod());
	}

	function setZeroTrustPeriod(uint blocks) external ifAdmin { // before this called acts like a normal eip 1967 transparent proxy
		assembly{ sstore(ZERO_TRUST_PERIOD_SLOT, blocks) }
		emit ZeroTrustPeriodSet(blocks);
	}
	function _updateBlockSlot() internal {
		uint nlb = block.number + _zeroTrustPeriod();
		assembly {sstore(NEXT_LOGIC_BLOCK_SLOT,nlb)}
	}

	function _setNextLogic(address nl) internal {
		require(block.number >= _proposeBlock(),"too soon");
		_updateBlockSlot();
		assembly { sstore(NEXT_LOGIC_SLOT, nl)}
		emit NextLogicDefined(nl,block.number + _zeroTrustPeriod());
	}

	function proposeTo(address newLogic, bytes calldata data) payable external ifAdmin {
		if (_zeroTrustPeriod() == 0) {
			_updateBlockSlot();
			assembly {sstore(LOGIC_SLOT,newLogic)}
			(bool success,) = newLogic.delegatecall(data);
			require(success,"failed to call");
			emit Upgraded(newLogic);
		} else{
			_setNextLogic(newLogic);
		}
	}

	function _delegate(address logic_) internal {
		assembly {
			calldatacopy(0, 0, calldatasize())
			let result := delegatecall(gas(), logic_, 0, calldatasize(), 0, 0)
			returndatacopy(0, 0, returndatasize())
			switch result
			case 0 { revert(0, returndatasize()) }
			default { return(0, returndatasize()) }
		}
	}
}
```

## Copyright
Copyright and related rights waived via [CC0](../LICENSE.md).
