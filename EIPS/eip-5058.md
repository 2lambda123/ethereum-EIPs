---
eip: 5058
title: Lockable ERC-721 Standard
description: An extension to the ERC-721 standard to enable a trustless locking mechanism, which is useful for use cases such as locking, staking, lending, or crowdfunding.
author: Tyler(@radiocaca), Alex(@gojazdev)
discussions-to: <URL>
status: Draft
type: Standards Track
category: ERC
created: 2022-04-30
requires (*optional): 165,721
---

## Abstract

We propose to extend the ERC-721 standard with a secure locking mechanism. The NFT owners approve the operator to lock the NFT through `setLockApprovalForAll()` or `lockApprove()`. The apporved operator locks the NFT through `lockFrom()`. The locked NFTs cannot be transferred until the end of the locking period. An immediate use case is to allow NFTs to participate in smart contracts without leaving the wallets of their owners.

## Motivation

NFTs, enabled by [ERC-721](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-721.md), has been on the explosion in demand. The total market value and the ecosystem continue to grow with more and more blue chip NFTs, which are approximately equivalent to popular intellectual properties in a conventional sense. Despite the vast success, something is left to be desired. Liquidity has always been one of the biggest challenges for NFTs. Several attempts have been made to tackle the liquidity challenge: [NFTFi](https://www.nftfi.com/) and [BendDAO](https://www.benddao.xyz/), to name a few. Utilizing the currently prevalent ERC-721 standard, these projects require participating NFTs to be transferred to the projects' contracts, which poses inconveniences and risks to the owners:

1. Smart contract risks: NFTs can be lost or stolen due to bugs or vulnerabilities in the contracts.
2. Loss of utility: NFTs have utility values, such as profile pictures and bragging rights, which are lost when the NFTs are no longer seen under the owners' custody.
3. Missing Airdrops: The owners can no longer directly receive airdrops entitled to the NFTs. Considering the values and price fluctuation of some of the airdrops, either missing or getting the airdrop not on time can financially impact the owners.

All-of-the-above are bad UX, and we believe the ERC-721 standard can be improved by adopting a native locking mechanism:

1. Instead of being transferred to a smart contract, a NFT remains being self-custody but locked.
2. While a NFT is locked, only the change of its ownership is disabled. Other utilities are not affected.
3. The owners can receive or claim airdrops themselves.

The proposed standard allows the underlying NFT assets to be managed securely and conveniently and extends the ERC-721 standard to natively support of common NFTFi use cases including locking, staking, lending, and crowdfunding. We believe the proposed standard will encourage NFT owners to participate more actively in NFTFi projects and, hence, improve the livelihood of the whole NFT ecosystem.

## Specification

The key words “MUST”, “MUST NOT”, “REQUIRED”, “SHALL”, “SHALL NOT”, “SHOULD”, “SHOULD NOT”, “RECOMMENDED”, “MAY”, and “OPTIONAL” in this document are to be interpreted as described in RFC 2119.

Lockable ERC-721 **MUST** implement the `IERC721Lockable` interfaces:

```solidity
// SPDX-License-Identifier: MIT
// Creator: tyler@radiocaca.com

pragma solidity ^0.8.8;

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";

/**
 * @dev ERC-721 Non-Fungible Token Standard, optional lockable extension
 * ERC721 Token that can be locked for a certain period and cannot be transferred.
 * This is designed for a non-escrow staking contract that comes later to lock a user's NFT
 * while still letting them keep it in their wallet.
 * This extension can ensure the security of user tokens during the staking period.
 * If the nft lending protocol is compatible with this extension, the trouble caused by the NFT
 * airdrop can be avoided, because the airdrop is still in the user's wallet
 */
interface IERC721Lockable is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is locked from `from`.
     */
    event Locked(address indexed operator, address indexed from, uint256 indexed tokenId, uint256 expired);

    /**
     * @dev Emitted when `tokenId` token is unlocked from `from`.
     */
    event Unlocked(address indexed operator, address indexed from, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to lock the `tokenId` token.
     */
    event LockApproval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to lock all of its tokens.
     */
    event LockApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the current locker of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function lockerOf(uint256 tokenId) external view returns (address locker);

    /**
     * @dev Lock `tokenId` token until `expired` from `from`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - `expired` must be greater than block.timestamp
     * - If the caller is not `from`, it must be approved to lock this token
     * by either {lockApprove} or {setLockApprovalForAll}.
     *
     * Emits a {Locked} event.
     */
    function lockFrom(
        address from,
        uint256 tokenId,
        uint256 expired
    ) external;

    /**
     * @dev Unlock `tokenId` token until `expired` from `from`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - the caller must be the operator who locks the token by {lockFrom}
     *
     * Emits a {Unlocked} event.
     */
    function unlockFrom(address from, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to lock `tokenId` token.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved lock operator.
     * - `tokenId` must exist.
     *
     * Emits an {LockApproval} event.
     */
    function lockApprove(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an lock operator for the caller.
     * Operators can call {lockFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {LockApprovalForAll} event.
     */
    function setLockApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns the account lock approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getLockApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to lock all of the assets of `owner`.
     *
     * See {setLockApprovalForAll}
     */
    function isLockApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Returns if the `tokenId` token is locked.
     */
    function isLocked(uint256 tokenId) external view returns (bool);
}

```

## Rationale

### NFT lock approvals

A NFT owner can give another trusted operator the right to lock his NFT through the approve functions. The `lockApprove()` function only approves for the specified NFT, whereas `setLockApprovalForAll()` approves for all NFTs of the collection under the wallet. When a user participates in a NFTFi project, the project contract calls `lockFrom()` to lock the user's NFT. Locked NFTs cannot be transferred, but the NFTFi project contract can use the unlock function `unlockFrom()` to unlock the NFT.

### NFT lock period

When locking a NFT, it is required to specify the lock expiration block height, which must be greater than the current block height. Upon the lock expiration, the NFT is automatically released and can be transferred.

### Bound NFT

While the underlying NFT asset is being locked in the owner's wallet, the NFT may still be expected to provide certain visibility to the locking contracts. We designed a bound NFT (boundNFT) to take this requirement into account.

When a contract locks a NFT, it can opt to mint a boundNFT. The boundNFT contains the original NFT's metadata and other information. A boundNFT does not have a transfer function. A boundNFT is mere a certificate. When the NFT asset is released, the bNFT will be atomically destroyed.

## Backwards Compatibility

This standard is compatible with current ERC-721 standards.

## Reference Implementation

You can find an implementation of this standard in [RadioCaca](https://github.com/radiocaca/ERC721L/tree/main/contracts/EIP5058).

## Security Considerations

There are no security considerations related directly to the implementation of this standard.

## Copyright

Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
