---
eip: 4973
title: Non-Transferrable Non-Fungible Tokens (Soulbound Tokens or "Badges")
description: A standard interface for non-transferrable non-fungible tokens, also known as "soulbound tokens" (short "SBT") or "badges".
author: Nicola Greco, Tim Daubensch√ºtz (TimDaub)
discussions-to: https://ethereum-magicians.org/t/eip-4973-non-transferrable-non-fungible-tokens-soulbound-tokens-or-badges/8825
status: Draft
type: Standards Track
category: ERC
created: 2022-04-01
requires: 165, 721
---

## Abstract

Proposes a standard API for Soulbound Tokens (SBTs), also known as Badges, within smart contracts. An SBT is a non-fungible token bound to a single entity, and cannot be transferred between entities. This EIP defines basic functionality to gift, mint, and track SBTs.

## Motivation

In various GitHub issues, pull requests, and Ethereum's governance forums, its community, has expressed a need for non-transferrable, non-fungible tokens. Sometimes those "NTTs" are referred to as "soulbound tokens" ("SBTs") in reference to popular MMORPGs such as World of Warcraft, where items, upon looting, are bound with the player's soul and hence could not subsequently be transferred to other characters anymore. Other times, when using them as credentials, NTTs have been called "badges."

The purpose of this document is to make NTTs a reality on Ethereum by creating consensus around a **maximally backward-compatible** but otherwise **minimal** interface definition.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119.

`ERC4973` tokens must define the interface function of [`ERC165`](./eip-165.md) "Standard Interface Definition." An `ERC4973` must support the [`ERC721`](./eip-721.md)'s `ERC721Metadata` interface with the hexadecimal identifier `0x5b5e139f`. It must not, however, support [`ERC721`](./eip-721.md)'s tracking and transferring functionality with the interface identifier `0x80ac58cd`. Neither [`ERC721`](./eip-721.md)'s `ERC721TokenReceiver` with the interface identifier `0x150b7a02` must be supported.

```solidity
pragma solidity ^0.8.6;

/// @title Non-Transferrable Non-Fungible Tokens (Soulbound Tokens or "Badges")
/// @dev See https://eips.ethereum.org/EIPS/eip-4973
///  Note: the ERC-165 identifier for this interface is 0x6352211e.
interface IERC4973 /* is ERC165, ERC721Metadata */ {
  /// @dev This emits when transfer of any soulbound token is established by any
  /// mechanism. This event emits when SBTs are created (`from` == 0) and
  /// destroyed (`to` == 0). Exception: during contract creation, any number of
  /// SBTs may be created and assigned without emitting Transfer.
  event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
  /// @notice Find the address bound to an ERC4973 soulbound token
  /// @dev SBTs assigned to zero address are considered invalid, and queries
  ///  about them do throw.
  /// @param _tokenId The identifier for an SBT
  /// @return The address of the owner bound to the SBT
  function ownerOf(uint256 _tokenId) external view returns (address);
}

interface ERC165 {
  /// @notice Query if a contract implements an interface
  /// @param interfaceID The interface identifier, as specified in ERC-165
  /// @dev Interface identification is specified in ERC-165. This function
  ///  uses less than 30,000 gas.
  /// @return `true` if the contract implements `interfaceID` and
  ///  `interfaceID` is not 0xffffffff, `false` otherwise
  function supportsInterface(bytes4 interfaceID) external view returns (bool);
}

/// @title ERC-721 Non-Fungible Token Standard, optional metadata extension
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x5b5e139f.
interface ERC721Metadata /* is ERC721 */ {
  /// @notice A descriptive name for a collection of NFTs in this contract
  function name() external view returns (string _name);

  /// @notice An abbreviated name for NFTs in this contract
  function symbol() external view returns (string _symbol);

  /// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
  /// @dev Throws if `_tokenId` is not a valid NFT. URIs are defined in RFC
  ///  3986. The URI may point to a JSON file that conforms to the "ERC721
  ///  Metadata JSON Schema".
  function tokenURI(uint256 _tokenId) external view returns (string);
}
```

For "ERC721 Metadata JSON Schema", see [`ERC721`](./eip-721.md).

## Rationale

`ERC4973` shall be maximally backward-compatible but still only expose a minimal and simple to implement interface definition.

As [`ERC721`](./eip-721.md) tokens have seen widespread adoption with wallet providers and marketplaces, using its `ERC721Metadata` interface with [`ERC165`](./eip-165.md) for feature-detection potentially allows implementers to support `ERC4973` tokens out of the box.

If an implementer of [`ERC721`](./eip-721.md) properly built [`ERC165`](./eip-165.md)'s `function supportsInterface(bytes4 interfaceID` function, already by recognizing that [`ERC721`](./eip-721.md)'s track and transfer interface component with the identifier `0x80ac58cd`, transferring of a token should not be suggested as a user interface option.

Still, however, since `ERC4973` supports [`ERC721`](./eip-721.md)'s `ERC721Metadata` extension, a "soulbound" token should be displayed in wallets and marketplaces with no changes needed.

Although other possible implementations of soulbound tokens are possible, e.g., by having all transfer functions revert, `ERC4973` is superior as it supports fine-grained feature detection through [`ERC165`](./eip-165.md).

## Backwards Compatibility

We have adopted the [`ERC165`](./eip-165.md) and `ERC721Metadata` functions purposefully to create a high degree of backward compatibility with [`ERC721`](./eip-721.md)'s implementers. Additionally, we have deliberately used [`ERC721`](./eip-721md`) terminology and definitions such as `event Transfer(...)` and `function ownerOf(...)` to minimize the effort for `ERC4973` implementers already familiar with, e.g., [`ERC20`](./eip-20.md) or [`ERC721`](./eip-721.md).

## Reference Implementation

You can find an implementation of this standard in [../assets/eip-4973](../assets/eip-4973).

## Copyright

Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
