---
eip: 4973
title: Non-Transferrable Non-Fungible Tokens (Soulbound Tokens)
description: A standard interface for non-transferrable non-fungible tokens, also known as "soulbound tokens" (short "SBT").
author: Nicola Greco, Tim Daubensch√ºtz (TimDaub)
discussions-to: https://ethereum-magicians.org/t/eip-4966-non-transferrable-non-fungible-tokens-soulbound-tokens/8825
status: Draft
type: Standards Track
category: ERC
created: 2022-04-01
requires: 165, 721
---

## Abstract

The following standard allows for the implementation of a standard API for SBTs within smart contracts. This standard provides basic functionality to gift, mint and track SBTs.

## Motivation

`ERC4973` tokens inhert their metadata model from [`ERC721`](./eip-721.md)'s metadata. Wallets can differentiate between `ERC4973` and transferrable [`ERC721`](./eip-721.md) tokens through [`ERC165`](./eip-165.md)'s `supportsInterface(bytes4 interfaceID);` method.

When a wallet detects support for `IERC721Metadata` (`interfaceID`: `0x5b5e139f`), it can additonal check if transfer functionality via

- `IERC721` (`interfaceID`: `0x80ac58cd`); or
- soulbound properties via `IERC4973` (`interfaceID`: `0xad5ec850`)

are supported.

Since `ERC4973` supports `ERC721Metadata`, if implemented correctly, [`ERC721`](./eip-721.md) wallets should be able to display `ERC4973` tokens without the need for any modifications from developers. [`ERC721`](./eip-721.md)'s transfer functionality could be hidden from the UI if `interfaceID` `0x80ac58cd` isn't supported or `ERC4973`'s `interfaceID` is detected.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119.

**Every `ERC4973` compliant contract must implement the `ERC4973`, `ERC721Metadata` and [`ERC165`](./eip-165.md) interfaces** (subject to "caveats" below):

```solidity
pragma solidity ^0.8.6;

/// @title ERC-4973 Non-Transferrable Non-Fungible Token Standard
/// @dev See https://eips.ethereum.org/EIPS/eip-4973
///  Note: the ERC-165 identifier for this interface is 0xad5ec850.
interface IERC4973 /* is ERC165, ERC721Metadata */ {
  /// @dev This emits when bond of any SBT is established by any mechanism.
  ///  This event emits when SBTs are created (`from` == 0) and destroyed
  ///  (`to` == 0). Exception: during contract creation, any number of SBTs
  ///  may be created and assigned without emitting Bond.
  event Bond(address indexed _from, address indexed _to, uint256 indexed _tokenId);
  /// @notice Find the address bound to an ERC4973 soulbound token (short "SBT")
  /// @dev SBTs assigned to zero address are considered invalid, and queries
  ///  about them do throw.
  /// @param _tokenId The identifier for an SBT
  /// @return The address of the owner bound to the SBT
  function boundTo(uint256 _tokenId) external view returns (address);
}

interface ERC165 {
  /// @notice Query if a contract implements an interface
  /// @param interfaceID The interface identifier, as specified in ERC-165
  /// @dev Interface identification is specified in ERC-165. This function
  ///  uses less than 30,000 gas.
  /// @return `true` if the contract implements `interfaceID` and
  ///  `interfaceID` is not 0xffffffff, `false` otherwise
  function supportsInterface(bytes4 interfaceID) external view returns (bool);
}

/// @title ERC-721 Non-Fungible Token Standard, optional metadata extension
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x5b5e139f.
interface ERC721Metadata /* is ERC721 */ {
  /// @notice A descriptive name for a collection of NFTs in this contract
  function name() external view returns (string _name);

  /// @notice An abbreviated name for NFTs in this contract
  function symbol() external view returns (string _symbol);

  /// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
  /// @dev Throws if `_tokenId` is not a valid NFT. URIs are defined in RFC
  ///  3986. The URI may point to a JSON file that conforms to the "ERC721
  ///  Metadata JSON Schema".
  function tokenURI(uint256 _tokenId) external view returns (string);
}
```

For "ERC721 Metadata JSON Schema", see [`ERC721`](./eip-721.md).

## Rationale

Since 2018, the Ethereum community has been seeking consensus for a standard for non-transferrable non-fungible token.

For `ERC4973`, we propose:

- `event Bond(address indexed _from, address indexed _to, uint256 indexed _tokenId)`, essentially `ERC721`'s Transfer event but renamed to "Bond".
- `function boundTo(uint256 _tokenId) external view returns (address)`, essentially `ERC721`'s `ownerOf(uint256 _tokenId)` but renamed to `boundTo`.

The benefit of using the `ERC721Metadata` interface is that implementers (e.g. wallets) of `ERC4973` can identify transfer and tracking functionality using the [`ERC165`](./eip-165.md) `supportsInterface(bytes4 interfaceID)` function.

## Backwards Compatibility

We have adopted the [`ERC165`](./eip-165.md) and `ERC721Metadata` functions. `ERC4973`'s `boundTo` function is the pendant to [`ERC721`](./eip-721.md)'s `ownerOf` function.

## Reference Implementation

You can find an implementation of this standard in [../assets/eip-4973](../assets/eip-4973).

## Copyright

Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
