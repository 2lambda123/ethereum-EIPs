---
eip: 4973
title: Account-bound Tokens
description: A standard interface for non-transferrable non-fungible tokens, also known as "account-bound" or "soulbound tokens" or "badges".
author: Nicola Greco, Tim Daubensch√ºtz (@TimDaub)
discussions-to: https://ethereum-magicians.org/t/eip-4973-non-transferrable-non-fungible-tokens-soulbound-tokens-or-badges/8825
status: Draft
type: Standards Track
category: ERC
created: 2022-04-01
requires: 165, 721
---

## Abstract

Proposes a standard API for account-bound Tokens (ACT), also known as Badges, within smart contracts. An ACT is a non-fungible token bound to a single account, and cannot be transferred between accounts. This EIP defines basic functionality to gift, mint, and track ACTs.

## Motivation

The Ethereum community has expressed a need for non-transferrable, non-fungible tokens. Common use cases include tracking an individual's achievements (which can be used as credentials), or items that cannot be transferred (like certain loot in multiplayer online games).

The purpose of this document is to make ACTs a reality on Ethereum by creating consensus around a **maximally backward-compatible** but otherwise **minimal** interface definition.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119.

`ERC-4973` tokens _must_ implement the interfaces:

- [ERC-165](./eip-165.md)'s `ERC165` (`0x01ffc9a7`)
- [ERC-721](./eip-721.md)'s `ERC721Metadata` (`0x5b5e139f`)

`ERC-4973` tokens _must not_ implement the interfaces:

- [ERC-721](./eip-721.md)'s `ERC721` (`0x80ac58cd`)

```solidity
pragma solidity ^0.8.6;

/// @title Non-Transferrable Non-Fungible Account-bound Tokens (formerly Soulbound Tokens or "Badges")
/// @dev See https://eips.ethereum.org/EIPS/eip-4973
///  Note: the ERC-165 identifier for this interface is 0x6352211e.
interface IERC4973 /* is ERC165, ERC721Metadata */ {
  /// @dev This emits when transfer of any account-bound token is established by
  /// any mechanism. This event emits when ACTs are created (`from` == 0) and
  /// destroyed or revoked (`to` == 0). Exception: during contract creation, any
  /// number of ACTs may be created and assigned without emitting Transfer.
  event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);
  /// @notice Find the address bound to an ERC4973 account-bound token
  /// @dev ACTs assigned to zero address are considered invalid, and queries
  ///  about them do throw.
  /// @param _tokenId The identifier for an ACT
  /// @return The address of the owner bound to the ACT
  function ownerOf(uint256 _tokenId) external view returns (address);
}

interface ERC165 {
  /// @notice Query if a contract implements an interface
  /// @param interfaceID The interface identifier, as specified in ERC-165
  /// @dev Interface identification is specified in ERC-165. This function
  ///  uses less than 30,000 gas.
  /// @return `true` if the contract implements `interfaceID` and
  ///  `interfaceID` is not 0xffffffff, `false` otherwise
  function supportsInterface(bytes4 interfaceID) external view returns (bool);
}

/// @title ERC-721 Non-Fungible Token Standard, optional metadata extension
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x5b5e139f.
interface ERC721Metadata /* is ERC721 */ {
  /// @notice A descriptive name for a collection of NFTs in this contract
  function name() external view returns (string _name);

  /// @notice An abbreviated name for NFTs in this contract
  function symbol() external view returns (string _symbol);

  /// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
  /// @dev Throws if `_tokenId` is not a valid NFT. URIs are defined in RFC
  ///  3986. The URI may point to a JSON file that conforms to the "ERC721
  ///  Metadata JSON Schema".
  function tokenURI(uint256 _tokenId) external view returns (string);
}
```

For "ERC721 Metadata JSON Schema", see [`ERC721`](./eip-721.md).

## Rationale

### Interface

`ERC4973` shall be maximally backward-compatible but still only expose a minimal and simple to implement interface definition.

As [`ERC721`](./eip-721.md) tokens have seen widespread adoption with wallet providers and marketplaces, using its `ERC721Metadata` interface with [`ERC165`](./eip-165.md) for feature-detection potentially allows implementers to support `ERC4973` tokens out of the box.

If an implementer of [`ERC721`](./eip-721.md) properly built [`ERC165`](./eip-165.md)'s `function supportsInterface(bytes4 interfaceID)` function, already by recognizing that [`ERC721`](./eip-721.md)'s track and transfer interface component with the identifier `0x80ac58cd` is not implemented, transferring of a token should not be suggested as a user interface option.

Still, however, since `ERC4973` supports [`ERC721`](./eip-721.md)'s `ERC721Metadata` extension, an account-bound token should be displayed in wallets and marketplaces with no changes needed.

Although other possible implementations of account-bound tokens are possible, e.g., by having all transfer functions revert, `ERC4973` is superior as it supports feature detection through [`ERC165`](./eip-165.md).

### Naming

To precisely identify the standard's concept, we're referring to tokens as "account-bound" rather than "soulbound." Calling ERC4973 tokens "soulbound" would have opened the possibility to overinterpret its functionality as being bound to "a human's soul" - an idea we're not setting out to solve here. Rather, ERC4973 tokens are tied to specific Ethereum accounts and are therefore called "account-bound."

### Exception handling

Given the non-transferable between accounts property of ACTs, if a user's keys to an account or a contract get compromised or rotated, a user may lose the ability to associate themselves with the token. In some cases, this can be the desired effect. Therefore, it is suggested to implement re-issuance and potential revocation processes on the issuer side to enable recourse when it is not.

This document is deliberately abstaining from offering a standardized form of exception handling in cases where user keys are compromised or rotated.

In cases where implementers want to make account-bound tokens sharable among different accounts, e.g., to avoid losing access when keys get compromised, we suggest issuing the account-bound token towards a contract's account that implements a multi-signature functionality.

### Revocations

Revocations are optionally implementable by allowing an issuer to emit a secondary `event Transfer` with `to == 0` after emitting the initial minting `event Transfer` with `from == 0`.

## Backwards Compatibility

We have adopted the [`ERC165`](./eip-165.md) and `ERC721Metadata` functions purposefully to create a high degree of backward compatibility with [`ERC721`](./eip-721.md)'s implementers. Additionally, we have deliberately used [`ERC721`](./eip-721md`) terminology and definitions such as `event Transfer(...)` and `function ownerOf(...)` to minimize the effort of familiarization for `ERC4973` implementers already familiar with, e.g., [`ERC20`](./eip-20.md) or [`ERC721`](./eip-721.md).

## Reference Implementation

You can find an implementation of this standard in [../assets/eip-4973](../assets/eip-4973).

## Copyright

Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
